#!/usr/bin/perl
use strict;
use warnings;

# JSON bookmark parser (for Firefox backups).

use Encode;
use File::Slurper 'read_text';
use IO::Prompt;
use JSON::MaybeXS;

my $file   = shift || die "Usage: perl $0 /some/bookmarks.json [prompt]\n";
my $prompt = shift // 1;

my $content   = read_text($file);
my $bookmarks = decode_json( encode( 'utf8', $content ) );

my $n = 0;

traverse($bookmarks);

sub traverse {
    my ($items) = @_;

    for my $i ( keys %$items ) {
        if ( $i eq 'uri' ) {
            $n++;

            if ( $prompt && $n % 10 == 0 ) {
                my $response = prompt "Enter=next q=quit:\n";
                exit if $response eq 'q';
            }

            print qq/$n. "$items->{title}" ($items->{id})\n\t$items->{uri}\n\n/;
        }
        elsif ( $i eq 'children' ) {
            print '-' x 80, "\n";
            print "FOLDER: '$items->{title}'\n\n";

            traverse($_) for @{ $items->{children} };
        }
    }
}

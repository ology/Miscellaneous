#!/usr/bin/env perl
use strict;
use warnings;

package WordBot;
use base qw(Bot::BasicBot);

use lib '/Users/gene/sandbox/Lingua-Word-Parser/lib';
use Lingua::Word::Parser;

sub help {
    my $self      = shift;
    my $arguments = shift;

    $self->say(
        channel => $arguments->{channel},
        body    => '',
    );
}

sub said {
    my $self      = shift;
    my $arguments = shift;

    my $body = '?';

    if ( $arguments->{address} ) {
        if ( $arguments->{body} =~ /^source$/ ) {
            $body = 'https://github.com/ology/Miscellaneous/blob/master/WordBot';
        }
        elsif ( $arguments->{body} =~ /^([a-zA-Z]+) ([a-zA-Z]+)$/ ) {
            my $op   = $1;  # Operation
            my $word = $2;

            my $p = Lingua::Word::Parser->new(
                word => $word,
                file => 'lexicon.dat',
            );

            # Crunch the numbers:
            my $known  = $p->knowns;
            my $combos = $p->power;
            my $score  = $p->score;

            my @parts;
            # For each partition of the final match...
            for my $part ( @{ $score->{ [ sort keys %$score ]->[-1] } } ) {
                # Choose the part based on the operation
                push @parts,
                    $op eq 'part'  ? $part->{partition}   :
                    $op eq 'def'   ? $part->{definition}  :
                    $op eq 'score' ? $part->{score}       :
                    $op eq 'fam'   ? $part->{familiarity} : '?';
            }

            $body = join ' | ', @parts;
        }

        $self->say(
            channel => $arguments->{channel},
            body    => $body,
        );
    }
}

package main;

my $bot = WordBot->new(
    server      => 'irc.perl.org',
    port        => '6667',
    channels    => ['#bottest'],
    nick        => 'WordBot',
    name        => 'Gene Boggs Bot',
    ignore_list => [],
);

$bot->run();

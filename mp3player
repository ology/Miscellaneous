#!/usr/bin/env perl
use strict;
use warnings;

use Data::Dumper::Compact 'ddc';
use Audio::Play::MPlayer;
use File::Find::Rule;
use List::Util qw(shuffle);
use MP3::Info;
use IO::Prompt::Tiny 'prompt';
use DateTime;

my $dir = shift || die "Usage: perl $0 /some/mp3/directory\n";

my $rule = File::Find::Rule->new;
my @files = $rule->or(
  $rule->new
    ->directory
    ->name(qr/(?:People|Spoken|Student)/)
    ->prune
    ->discard,
  $rule->new
    ->file()
    ->name('*.mp3'),
)->in($dir);
@files = shuffle @files;
print "Files found under $dir: ", scalar(@files), "\n";

my $player = Audio::Play::MPlayer->new;

my $i = 0;
for my $file (@files) {
  $i++;

  my $mp3 = MP3::Info->new($file);
  print "$i. $file (", $mp3->time, ') ', $mp3->genre, "\n";

  my $input = prompt('[n-next q-quit]');
  if ($input eq 'q') {
    last;
  }
  elsif ($input eq 'n') {
    next;
  }
  else {
    my @time = reverse split /:/, $mp3->time;
    my $dt = DateTime->now(time_zone => 'local')->add(seconds => $time[0]);
    $dt->add(minutes => $time[1]) if defined $time[1];
    $dt->add(hours => $time[2]) if defined $time[2];
    print '... Now playing until ', $dt->hms, " ...\n";
    $player->load($file);
    while ($player->state || time() < $dt->epoch) {
      $player->poll(1);
    }
  }
}

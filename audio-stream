#!/usr/bin/env perl

use File::Find::Rule;
use Mojolicious::Lite -signatures;
use Number::Format;
use Storable qw(retrieve store);

use constant DAT => "$0.dat";
use constant PATH => 'public/Audio/';

get '/' => sub ($c) {
  my $autoplay = $c->param('autoplay') || 0;
  my $current = $c->param('current') || 0;
  my $shuffle = $c->param('shuffle') || 0;
  my $query = $c->param('query') || '';
  my $music = [];
  my $match = [];
  $music = retrieve(DAT) if -e DAT;
  $current = $shuffle ? int rand @$music : $current + 1;
  my $tune = $music->[$current];
  my $nf = Number::Format->new;
  my $total = $nf->format_number(scalar @$music);
  if ($query) {
    for my $n (0 .. $#$music) {
      push @$match, $n if $music->[$n] =~ /$query/;
    }
  }
  $c->render(
    template => 'index',
    total => $total,
    music => $music,
    tune => $tune,
    autoplay => $autoplay,
    current => $current,
    shuffle => $shuffle,
    query => $query,
    match => $match,
  );
} => 'index';

get '/refresh' => sub ($c) {
  my @files = File::Find::Rule->file()
                              ->name('*.mp3', '*.m4a')
                              ->in(PATH);
  for my $file (@files) {
    $file =~ s/^public//;
  }
  store \@files, DAT;
  $c->redirect_to($c->url_for('index'));
} => 'refresh';

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Music!';
<b>Autoplay:</b> <input type="checkbox" id="autoplay" name="autoplay" <%= $autoplay ? 'checked' : '' %> />
<b>Shuffle:</b> <input type="checkbox" id="shuffle" name="shuffle" <%= $shuffle ? 'checked' : '' %> />
<p></p>
<b>Total:</b> <%= $total %> tracks
<p></p>
<b>Tune:</b> <%= $tune %>
<p></p>
<audio controls id='myAudio' <%= $autoplay ? "autoplay='autoplay'" : '' %> preload="auto" type="audio/mpeg" crossorigin="anonymous" src="">
  Your browser does not support the <code>audio</code> element.
</audio>
<p></p>
<a href="<%= url_for('refresh') %>">Reread Audio</a>
<p></p>
<form method="get">
  <label for="query"><b>Search:</b></label>
  <input type="hidden" id="autoplay" name="autoplay" value="0" />
  <input type="hidden" id="shuffle" name="shuffle" value="0" />
  <input type="hidden" id="autoplay" name="autoplay" value="0" />
  <input type="hidden" id="current" name="current" value="<%= $current %>" />
  <input type="text" id="query" name="query" value="<%= $query %>" />
  <input type="submit" value="Submit" />
</form>
% if (@$match) {
%   for my $n (@$match) {
      <p><a href="/?current=<%= $n - 1 %>&shuffle=0&autoplay=0&query=<%= $query %>"><%= $music->[$n] %></a></p>
%   }
% }
<script>
$(document).ready(function() {
  function relocate () {
    var x = 0;
    if ($('#shuffle').is(':checked')) {
      x = 1;
    }
    var y = 0;
    if ($('#autoplay').is(':checked')) {
      y = 1;
    }
    window.location = '/?current=' + <%= $current %> + '&shuffle=' + x + '&autoplay=' + y + '&query=' + "<%= $query %>";
  }
  $('#myAudio').attr('src', '<%= $tune %>');
  $('#myAudio').on('error', function() {
    console.log('Playback error! Skipping...');
    relocate();
  });
  $('#myAudio').on('playing', function() {
    console.log('Playing');
  });
  $('#myAudio').on('ended', function() {
    console.log('Ended');
    relocate();
  });
});
</script>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title><%= title %></title>
    <script src="https://code.jquery.com/jquery-3.6.0.min.js" integrity="sha256-/xUj+3OJU5yExlq6GSYGSHk7tPXikynS7ogEvDej/m4=" crossorigin="anonymous"></script>
  </head>
  <body><%= content %></body>
</html>

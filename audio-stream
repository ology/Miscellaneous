#!/usr/bin/env perl

# Lightweight, ultra-simple music server

# To run, symlink your audio source in a public subdirectory:
# audio-stream/public $ ln -s /media/you/external_drive/Audio/
# Then:
# audio-stream $ morbo audio-stream

use File::Find::Rule;
use Mojolicious::Lite -signatures;
use Number::Format;
use Storable qw(retrieve store);

use constant DAT => "$0.dat";
use constant PATH => 'public/Audio/';

get '/' => sub ($c) {
  my $autoadvance = $c->param('autoadvance') || 0;
  my $autoplay    = $c->param('autoplay') || 0;
  my $current     = $c->param('current') || 0;
  my $shuffle     = $c->param('shuffle') || 0;
  my $query       = $c->param('query') || '';
  my $submit      = $c->param('submit') || '';

  my $music = []; # Bucket for all tracks
  my $match = []; # Bucket for all matches if given a query

  $music = retrieve(DAT) if -e DAT;

  my $nf = Number::Format->new;
  my $total = $nf->format_number(scalar @$music);

  if ($query) {
    # Brute force through every track, looking for matches
    for my $n (0 .. $#$music) {
      push @$match, $n if lc($music->[$n]) =~ /$query/i;
    }

    # If shuffling, get a random member of the matches, otherwise increment
    $current = $shuffle ? $match->[int rand @$match] : $current + 1;
  }
  else {
    # If shuffling, get a random music track index, otherwise increment
    $current = $shuffle ? int(rand @$music) : $current + 1;
  }

  # Do not have a track when a search has been freshly submitted
  my $track = $submit ? '' : $music->[$current];

  $c->render(
    template    => 'index',
    total       => $total,
    music       => $music,
    track       => $track,
    autoplay    => $autoplay,
    autoadvance => $autoadvance,
    current     => $current,
    shuffle     => $shuffle,
    query       => $query,
    match       => $match,
  );
} => 'index';

get '/refresh' => sub ($c) {
  my @files = File::Find::Rule->file()
                              ->name('*.mp3', '*.m4a')
                              ->in(PATH);
  # Remove "public" from the filename
  for my $file (@files) {
    $file =~ s/^public//;
  }
  # Save the files to disk
  store \@files, DAT;

  $c->redirect_to($c->url_for('index'));
} => 'refresh';

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Music!';
<p>
  <label for="autoadvance"><b>Autoadvance:</b></label>
  <input type="checkbox" id="autoadvance" name="autoadvance" title="Automatically advance to the next track" <%= $autoadvance ? 'checked' : '' %> />
  <label for="autoplay"><b>Autoplay:</b></label>
  <input type="checkbox" id="autoplay" name="autoplay" title="Automatically start playing the current track" <%= $autoplay ? 'checked' : '' %> />
  <label for="shuffle"><b>Shuffle:</b></label>
  <input type="checkbox" id="shuffle" name="shuffle" title="Select a random track" <%= $shuffle ? 'checked' : '' %> />
</p>
<p><b>Total:</b> <%= $total %> tracks</p>
<p><b>Track:</b> <%= $track %></p>
<p>
<div>
  <audio controls id='myAudio' <%= $autoplay ? "autoplay='autoplay'" : '' %> preload="auto" type="audio/mpeg" crossorigin="anonymous" style="vertical-align: middle;" src="">
    Your browser does not support the <code>audio</code> element.
  </audio>
  <div id="warning" style="border: 1px solid red; color: red; display: inline-block;"><b>Could not load audio!</b></div>
</div>
</p>
<p>
  <a href="" onclick="linkout(<%= $current %>); return false;"><b>Next track</b></a>
  |
  <a href="<%= url_for('refresh') %>">Reread audio</a>
</p>
<form method="get">
  <input type="hidden" name="autoadvance" value="0" />
  <input type="hidden" name="autoplay" value="0" />
  <input type="hidden" name="shuffle" value="0" />
  <input type="hidden" name="current" value="<%= $current %>" />
  <label for="query"><b>Search:</b></label>
  <input type="text" id="query" name="query" value="<%= $query %>" />
  <input type="submit" name="submit" value="Submit" />
  <input type="button" onclick="location.href='/?query=&autoadvance=1&autoplay=1&shuffle=1';" value="Clear & Go" />
</form>
% if (@$match) {
    <p><b>Matches:</b> <%= scalar @$match %></p>
    <ol>
%   for my $n (@$match) {
    <li>
%     if ($track eq $music->[$n]) {
      <b><a href="" onclick="linkout(<%= $n - 1 %>); return false;"><%= $music->[$n] %></a></b>
%     } else {
      <a href="" onclick="linkout(<%= $n - 1 %>); return false;"><%= $music->[$n] %></a>
%     }
    </li>
%   }
    </ol>
% } else {
%   if ($query) {
    <p>No matches</p>
%   }
% }
<script>
// XXX This function linkout() does not work when under document-ready.
// TODO Consolidate the redundant "checked" bits with the relocate() function?
function linkout (n) {
  var x = 0;
  var shuf = document.querySelector('#shuffle');
  if (shuf.checked) {
    x = 1;
  }
  var y = 0;
  var autop = document.querySelector('#autoplay');
  if (autop.checked) {
    y = 1;
  }
  var z = 0;
  var autoa = document.querySelector('#autoadvance');
  if (autoa.checked) {
    z = 1;
  }
  var link = '/?current=' + n + '&shuffle=' + x + '&autoplay=' + y + '&autoadvance=' + z + '&query=' + "<%= $query %>";
  window.location = link
}
$(document).ready(function() {
  function relocate () {
    var x = 0;
    if ($('#shuffle').is(':checked')) {
      x = 1;
    }
    var y = 0;
    if ($('#autoplay').is(':checked')) {
      y = 1;
    }
    var z = 0;
    if ($('#autoadvance').is(':checked')) {
      z = 1;
      window.location = '/?current=' + <%= $current %> + '&shuffle=' + x + '&autoplay=' + y + '&autoadvance=' + z + '&query=' + "<%= $query %>";
    }
  }
  $('#warning').hide();
  $('#myAudio').attr('src', '<%= $track %>');
  $('#myAudio').on('error', function() {
    if ('<%= $track %>') {
      console.log('Playback error!');
      $('#warning').show();
      relocate();
    }
  });
  $('#myAudio').on('playing', function() {
    console.log('Playing...');
  });
  $('#myAudio').on('ended', function() {
    console.log('...Ended');
    relocate();
  });
});
</script>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title><%= title %></title>
    <script src="https://code.jquery.com/jquery-3.6.0.min.js" integrity="sha256-/xUj+3OJU5yExlq6GSYGSHk7tPXikynS7ogEvDej/m4=" crossorigin="anonymous"></script>
  </head>
  <body><%= content %></body>
</html>

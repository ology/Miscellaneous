#!/usr/bin/env perl

use File::Find::Rule;
use Mojolicious::Lite -signatures;
use Number::Format;
use Storable qw(retrieve store);

use constant DAT => 'music-stream.dat';
use constant PATH => 'public/Audio/';

get '/' => sub ($c) {
  my $current = $c->param('current') || 0;
  my $shuffle = $c->param('shuffle') || 0;
  my $query = $c->param('query') || '';
  my $music = [];
  $music = retrieve(DAT) if -e DAT;
  $current = $shuffle ? int rand @$music : $current + 1;
  my $tune = $music->[$current];
  my $nf = Number::Format->new;
  my $total = $nf->format_number(scalar @$music);
  $c->render(
    template => 'index',
    total => $total,
    tune => $tune,
    query => $query,
    current => $current,
    shuffle => $shuffle,
  );
} => 'index';

get '/refresh' => sub ($c) {
  my @files = File::Find::Rule->file()
                              ->name('*.mp3', '*.m4a')
                              ->in(PATH);
  for my $file (@files) {
    $file =~ s/^public//;
  }
  store \@files, DAT;
  $c->redirect_to($c->url_for('index'));
} => 'refresh';

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Music!';
<b>Shuffle:</b> <input type="checkbox" id="shuffle" name="shuffle" <%= $shuffle ? 'checked' : '' %> />
<p></p>
<b>Total:</b> <%= $total %> tracks
<p></p>
<b>Tune:</b> <%= $tune %>
<p></p>
<audio controls id='myAudio' autoplay='autoplay' preload="auto" type="audio/mpeg" crossorigin="anonymous" src="">
  Your browser does not support the <code>audio</code> element.
</audio>
<p></p>
<a href="<%= url_for('index') %>">Next tune</a>
|
<a href="<%= url_for('refresh') %>">Reread Audio</a>
<p></p>
<form>
  <label for="query"><b>Search:</b></label>
  <input type="text" id="query" name="query" value="<%= $query %>" />
  <input type="submit" value="Submit" />
</form>
<script>
$(document).ready(function() {
  function relocate () {
    var x = 0;
    if ($('#shuffle').is(':checked')) {
      x = 1;
    }
    window.location = '/?current=' + <%= $current %> + '&shuffle=' + x;
  }
  $('#myAudio').attr('src', '<%= $tune %>');
  $('#myAudio').on('error', function() {
    console.log('Playback error! Skipping...');
    relocate();
  });
  $('#myAudio').on('playing', function() {
    console.log('Playing');
  });
  $('#myAudio').on('ended', function() {
    console.log('Ended');
    relocate();
  });
});
</script>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
    <title><%= title %></title>
    <script src="https://code.jquery.com/jquery-3.6.0.min.js" integrity="sha256-/xUj+3OJU5yExlq6GSYGSHk7tPXikynS7ogEvDej/m4=" crossorigin="anonymous"></script>
  </head>
  <body><%= content %></body>
</html>

#!/usr/bin/env perl
use strict;
use warnings;

package Bot;
use base qw(Bot::BasicBot);

use Date::Manip;
use DateTime;
use DateTime::Format::DateParse;

sub help {
    my $self      = shift;
    my $arguments = shift;

    $self->say(
        channel => $arguments->{channel},
        body    => 'source|now|localtime $stamp|dow $stamp|diff $stamp $stamp|{add,sub}_{years,months,days} $offset $stamp',
    );
}

sub said {
    my $self      = shift;
    my $arguments = shift;

    my $body;

    my $re = qr/(\S+(?:\s+[\d:]+['"])?)/;
    my $format = '%Y-%m-%dT%H:%M:%S';

    if ( $arguments->{raw_body} =~ /^TimeBot:/ ) {
        # Return the source code link
        if ( $arguments->{body} =~ /^source$/ ) {
            $body = 'https://github.com/ology/Miscellaneous/blob/master/TimeBot';
        }
        # Return the current time
        elsif ( $arguments->{body} =~ /^now$/ ) {
            $body = DateTime->now( time_zone => 'local' );
        }
        # Return the localtime string of a given timestamp
        elsif ( $arguments->{body} =~ /^localtime $re$/ ) {
            my $capture = $1;
            $capture =~ s/['"]//g;
            $body = scalar( localtime UnixDate( $capture, '%s') );
        }
        # Return the day of the week of a given timestamp
        elsif ( $arguments->{body} =~ /^dow $re$/ ) {
            my $capture = $1;
            $capture =~ s/['"]//g;
            my $stamp = UnixDate( $capture, $format );
            my $dt = DateTime::Format::DateParse->parse_datetime($stamp);

            $body = $dt->day_name;
        }
        # Return the difference between two given timestamps
        elsif ( $arguments->{body} =~ /^diff $re $re$/ ) {
            my $capture1 = $1;
            $capture1 =~ s/['"]//g;
            my $capture2 = $2;
            $capture2 =~ s/['"]//g;

            my $stamp1 = UnixDate( $capture1, $format );
            my $dt1 = DateTime::Format::DateParse->parse_datetime($stamp1);
            my $stamp2 = UnixDate( $capture2, $format );
            my $dt2 = DateTime::Format::DateParse->parse_datetime($stamp2);

            $body = sprintf '%dy %dm %dd or %dh %dm %ds',
                $dt1->delta_md($dt2)->years,
                $dt1->delta_md($dt2)->months,
                $dt1->delta_md($dt2)->days,
                $dt1->delta_ms($dt2)->hours,
                $dt1->delta_ms($dt2)->minutes,
                $dt1->delta_ms($dt2)->seconds;
        }
        # Return the addition or subtraction of the given span and offset from the given timestamp
        elsif ( $arguments->{body} =~ /^([a-zA-Z]+)_([a-zA-Z]+) (\d+) $re$/ ) {
            my $method = $1;
            my $span = $2;

            $method = 'subtract' if $method eq 'sub';

            my $capture = $4;
            $capture =~ s/['"]//g;

            my $stamp = UnixDate( $capture, $format );
            my $dt = DateTime::Format::DateParse->parse_datetime($stamp);

            $body = $dt->$method( $span => $3 );
        }

        $self->say(
            channel => $arguments->{channel},
            body    => $body,
        );
     
#        $self->shutdown('I have done my job here.');
    }
}
 
package main;
 
my $bot = Bot->new(
    server      => 'irc.perl.org',
    port        => '6667',
    channels    => ['#bottest'],
    nick        => 'TimeBot',
    name        => 'Gene Boggs Bot',
    ignore_list => [],
);

$bot->run();
